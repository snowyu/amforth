# Comparision operators

# -----------------------------------------------------------------------------
  CODEWORD Flag_foldable_1,"0=",ZERO_EQUAL 
# ( x -- ? )
# -----------------------------------------------------------------------------
  sltiu x3, x3, 1
  addi x3, x3, -1
  xori x3, x3, -1
  NEXT

# -----------------------------------------------------------------------------
  CODEWORD Flag_foldable_1|Flag_inline, "0<>", NOTZEROEQUAL # ( x -- ? )
# -----------------------------------------------------------------------------
  sltiu x3, x3, 1
  addi x3, x3, -1
  NEXT

# -----------------------------------------------------------------------------
  CODEWORD Flag_foldable_1|Flag_inline, "0<", LESSZERO # ( n -- ? )
# -----------------------------------------------------------------------------
  srai x3, x3, 31
  NEXT

# -----------------------------------------------------------------------------
  CODEWORD Flag_foldable_0, "true", TRUE # ( -- -1 )
# -----------------------------------------------------------------------------
  pushdaconst -1
  NEXT

# -----------------------------------------------------------------------------
  CODEWORD Flag_foldable_0, "false", FALSE # ( x -- 0 )
# -----------------------------------------------------------------------------
  pushdaconst 0
  NEXT

# -----------------------------------------------------------------------------
  CODEWORD Flag_foldable_2, ">=", GREATEREQUAL # ( x1 x2 -- ? )
# -----------------------------------------------------------------------------
  lw x5, 0(x4)
  addi x4, x4, 4
  slt x3, x5, x3
  addi x3, x3, -1
  NEXT

# -----------------------------------------------------------------------------
  CODEWORD Flag_foldable_2, "<=", LESSEQUAL # ( x1 x2 -- ? )          
# -----------------------------------------------------------------------------
  lw x5, 0(x4)
  addi x4, x4, 4
  slt x3, x3, x5
  addi x3, x3, -1
  NEXT

# -----------------------------------------------------------------------------
  CODEWORD Flag_foldable_2, "<", LESS # ( x1 x2 -- ? )
                      # Checks if x2 is less than x1.
# -----------------------------------------------------------------------------
  lw x5, 0(x4)
  addi x4, x4, 4
  slt x3, x5, x3
  addi x3, x3, -1
  xori x3, x3, -1
  NEXT

# -----------------------------------------------------------------------------
  CODEWORD Flag_foldable_2, ">", GREATER # ( x1 x2 -- ? )
                      # Checks if x2 is greater than x1.
# -----------------------------------------------------------------------------
  lw x5, 0(x4)
  addi x4, x4, 4
  slt x3, x3, x5
  addi x3, x3, -1
  xori x3, x3, -1
  NEXT

# -----------------------------------------------------------------------------
  CODEWORD Flag_foldable_2, "u>=", UGREATEREQUAL # ( u1 u2 -- ? )
# -----------------------------------------------------------------------------
  lw x5, 0(x4)
  addi x4, x4, 4
  sltu x3, x5, x3
  addi x3, x3, -1
  NEXT

# -----------------------------------------------------------------------------
  CODEWORD Flag_foldable_2, "u<=", ULESSEQUAL # ( u1 u2 -- ? )
# -----------------------------------------------------------------------------
  lw x5, 0(x4)
  addi x4, x4, 4
  sltu x3, x3, x5
  addi x3, x3, -1
  NEXT

# -----------------------------------------------------------------------------
  CODEWORD Flag_foldable_2, "u<", ULESS # ( u1 u2 -- ? )
# -----------------------------------------------------------------------------
  lw x5, 0(x4)
  addi x4, x4, 4
  sltu x3, x5, x3
  addi x3, x3, -1
  xori x3, x3, -1
  NEXT

# -----------------------------------------------------------------------------
  CODEWORD Flag_foldable_2, "u>", UGREATER # ( u1 u2 -- ? )
# -----------------------------------------------------------------------------
  lw x5, 0(x4)
  addi x4, x4, 4
  sltu x3, x3, x5
  addi x3, x3, -1
  xori x3, x3, -1
  NEXT

# -----------------------------------------------------------------------------
  # Definition Flag_inline|Flag_opcodierbar_GleichUngleich, "<>" # ( x1 x2 -- ? )
  CODEWORD Flag_foldable_2, "<>", NOTEQUAL # ( x1 x2 -- ? )

                       # Compares the top two stack elements for inequality.
# -----------------------------------------------------------------------------
  lw x5, 0(x4)
  addi x4, x4, 4
  xor x3, x3, x5
  sltiu x3, x3, 1
  addi x3, x3, -1
  NEXT

# -----------------------------------------------------------------------------
  # Definition Flag_inline|Flag_opcodierbar_GleichUngleich, "=" # ( x1 x2 -- ? )
  CODEWORD Flag_foldable_2, "=", EQUAL # ( x1 x2 -- ? )
                      # Compares the top two stack elements for equality.
# -----------------------------------------------------------------------------
equal_einsprung:
  lw x5, 0(x4)
  addi x4, x4, 4
  xor x3, x3, x5
  sltiu x3, x3, 1
  addi x3, x3, -1
  xori x3, x3, -1
  NEXT

# -----------------------------------------------------------------------------
  CODEWORD Flag_foldable_2, "min", MIN # ( x1 x2 -- x3 )
                        # x3 is the lesser of x1 and x2.
# -----------------------------------------------------------------------------
  lw x5, 0(x4)
  addi x4, x4, 4
  blt x3, x5, 1f
    mv x3, x5
1:NEXT

# -----------------------------------------------------------------------------
  CODEWORD Flag_foldable_2, "max", MAX # ( x1 x2 -- x3 )
                        # x3 is the greater of x1 and x2.
# -----------------------------------------------------------------------------
  lw x5, 0(x4)
  addi x4, x4, 4
  blt x5, x3, 1f
    mv x3, x5
1:NEXT

# -----------------------------------------------------------------------------
  CODEWORD Flag_foldable_2, "umin", UMIN # ( u1 u2 -- u1|u2 )
# -----------------------------------------------------------------------------
  lw x5, 0(x4)
  addi x4, x4, 4
  bltu x3, x5, 1f
    mv x3, x5
1:NEXT

# -----------------------------------------------------------------------------
  CODEWORD Flag_foldable_2, "umax", UMAX # ( u1 u2 -- u1|u2 )
# -----------------------------------------------------------------------------
  lw x5, 0(x4)
  addi x4, x4, 4
  bltu x5, x3, 1f
    mv x3, x5
1:NEXT
